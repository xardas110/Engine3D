#version 460 core

//https://www.youtube.com/watch?v=z03vg2QTA8k&t=2053s&ab_channel=OREONENGINE

layout(local_size_x = 16, local_size_y = 16) in;

layout(binding = 0, rgba32f) uniform writeonly image2D normalmap;

uniform sampler2D heightmap;
uniform float strength;
uniform int N;

void main()
{
	ivec2 x = ivec2(gl_GlobalInvocationID.xy);
	vec2 texCoord = gl_GlobalInvocationID.xy/float(N);

	float texelSize = 1.f/N;

	float z0 = texture(heightmap, texCoord + vec2(-texelSize,-texelSize)).r;
	float z1 = texture(heightmap, texCoord + vec2(0,-texelSize)).r;
	float z2 = texture(heightmap, texCoord + vec2(texelSize,-texelSize)).r;
	float z3 = texture(heightmap, texCoord + vec2(-texelSize,0)).r;
	float z4 = texture(heightmap, texCoord + vec2(texelSize,0)).r;
	float z5 = texture(heightmap, texCoord + vec2(-texelSize,texelSize)).r;
	float z6 = texture(heightmap, texCoord + vec2(0,texelSize)).r;
	float z7 = texture(heightmap, texCoord + vec2(texelSize,texelSize)).r;
	 
	vec3 normal;

	//Sobel filter

	normal.z = 1.f/strength;

	normal.x = z0 + 2.f*z3 + z5 - z2 - 2.f*z4 - z7;
	normal.y = z0 + 2.f*z1 + z2 -z5 - 2.f*z6 - z7;

	imageStore(normalmap, x, vec4(normalize(normal), 1.f));
}
