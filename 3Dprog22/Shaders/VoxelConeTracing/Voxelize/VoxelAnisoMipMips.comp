#version 460 core
//Used Jose's approach for anisotropic filtering
//https://github.com/jose-villegas/VCTRenderer

layout (local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

layout (rgba8, binding = 0) uniform writeonly image3D target[6];
layout (binding = 5) uniform sampler3D source[6];

uniform int mipLevel;

const ivec3 anisoOffsets[] = ivec3[8]
(
	ivec3(1, 1, 1),
	ivec3(1, 1, 0),
	ivec3(1, 0, 1),
	ivec3(1, 0, 0),
	ivec3(0, 1, 1),
	ivec3(0, 1, 0),
	ivec3(0, 0, 1),
	ivec3(0, 0, 0)
);

void FetchTexels(ivec3 pos, int dir, inout vec4 val[8]) 
{
	for(int i = 0; i < 8; i++)
	{
		val[i] = texelFetch(source[dir], pos + anisoOffsets[i], mipLevel);
	}
}

void main(void)
{
	ivec3 pixel = ivec3(gl_GlobalInvocationID);

	ivec3 cp = pixel * 2;

	vec4 values[8];
	// x -
	FetchTexels(cp, 0, values);
	imageStore(target[0], pixel, 
	(
		values[0] + values[4] * (1 - values[0].a) + 
		values[1] + values[5] * (1 - values[1].a) +
		values[2] + values[6] * (1 - values[2].a) +
		values[3] + values[7] * (1 - values[3].a)) * 0.25f
	);
	// x +
	FetchTexels(cp, 1, values);
    imageStore(target[1], pixel, 
	(
		values[4] + values[0] * (1 - values[4].a) +
    	values[5] + values[1] * (1 - values[5].a) +
    	values[6] + values[2] * (1 - values[6].a) +
    	values[7] + values[3] * (1 - values[7].a)) * 0.25f
    );
	// y -	
	FetchTexels(cp, 2, values);
    imageStore(target[2], pixel, 
	(
		values[0] + values[2] * (1 - values[0].a) +
    	values[1] + values[3] * (1 - values[1].a) +
    	values[5] + values[7] * (1 - values[5].a) +
    	values[4] + values[6] * (1 - values[4].a)) * 0.25f
    );
	// y +
	FetchTexels(cp, 3, values);
    imageStore(target[3], pixel, 
	(
		values[2] + values[0] * (1 - values[2].a) +
    	values[3] + values[1] * (1 - values[3].a) +
    	values[7] + values[5] * (1 - values[7].a) +
    	values[6] + values[4] * (1 - values[6].a)) * 0.25f
    );
	// z -
	FetchTexels(cp, 4, values);
    imageStore(target[4], pixel, 
	(
		values[0] + values[1] * (1 - values[0].a) +
    	values[2] + values[3] * (1 - values[2].a) +
    	values[4] + values[5] * (1 - values[4].a) +
    	values[6] + values[7] * (1 - values[6].a)) * 0.25f
    );
	// z +
	FetchTexels(cp, 5, values);
    imageStore(target[5], pixel, 
	(
		values[1] + values[0] * (1 - values[1].a) +
    	values[3] + values[2] * (1 - values[3].a) +
    	values[5] + values[4] * (1 - values[5].a) +
    	values[7] + values[6] * (1 - values[7].a)) * 0.25f
    );
}